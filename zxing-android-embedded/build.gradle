apply plugin: 'com.android.library'
apply plugin: 'maven-publish'


ext.artifactId = 'zxing-android-embedded'


android {
    resourcePrefix 'zxing_'
    compileSdkVersion 29

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res-orig', 'res']
            assets.srcDirs = ['assets']
        }
        test.setRoot('test');
    }

    // This is bad practice - we should fix the warnings instead.
    lintOptions {
        // Android warns about the he and id locale folders. he -> iw is already handled with a
        // symlink. TODO: what about id?
        disable 'LocaleFolder'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        // We test with primitives such as Rect, and rely on their default behaviour working.
        unitTests.returnDefaultValues = true
    }
    defaultConfig {
        minSdkVersion 14
    }

    buildTypes {
        debug {
            versionNameSuffix ".debug"
            resValue "string", "app_version", "${defaultConfig.versionName}${versionNameSuffix}"
        }
        release {
            resValue "string", "app_version", "${defaultConfig.versionName}"
        }
    }
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

dependencies {
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.fragment:fragment:1.1.0'
    implementation 'com.google.zxing:core:3.3.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
}

project.afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                artifact bundleReleaseAar
                artifactId project.artifactId

                artifact sourceJar

                pom.withXml {
                    // HACK to add dependencies to POM.
                    // When maven-publish can do this automatically for Android projects,
                    // remove this section.
                    def deps = asNode().appendNode('dependencies')

                    project.configurations.api.allDependencies.each { dep ->
                        def node = deps.appendNode('dependency')
                        node.appendNode('groupId', dep.group)
                        node.appendNode('artifactId', dep.name)
                        node.appendNode('version', dep.version)
                        node.appendNode('scope', 'api')
                    }

                }
            }
        }
    }
}
